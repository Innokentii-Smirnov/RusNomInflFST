Multichar_Symbols +Subst1 +Subst2 +Adj +Pron1 +Pron2 +Poss +Nul +Refl +Masc +Fem +Neut +Sing +Plur +Nom +Acc +Gen +Loc +Dat +Ins +Stem +Ending @P.REFL.ON@ @R.REFL.ON@ @D.REFL.ON@
Definitions
	Gender = %+Masc|%+Fem|%+Neut;
	Number = %+Sing|%+Plur;
	Case = %+Nom|%+Acc|%+Gen|%+Loc|%+Dat|%+Ins;
	Subst = %+Subst1 | %+Subst2;
	Sep = "|";
	Soft = ж|ш|ч|щ|';
	B = Soft Sep;
	A = \Soft Sep;

LEXICON Root
+Subst1:0 		Subst1;
+Subst2:0 		Subst2;
+Adj:0 			Adj;
+Pron1:0 		Pron1;
+Pron2:0 		Pron2;
+Poss:0 		Poss;
+Nul:0			Nul;
+Refl:0			Refl;
Allomorphic;

LEXICON Allomorphic
<A [Subst %+Masc %+Plur %+Gen]:{ов}>						#;
<B [Subst %+Masc %+Plur %+Gen]:{еj}>						#;
<A [Subst:0 [%+Fem | %+Neut] %+Plur %+Gen]>					#;
<B [%+Subst1 [%+Fem | %+Neut] %+Plur %+Gen]:0 %+Stem>		#;
<B [%+Subst1 [%+Fem | %+Neut] %+Plur %+Gen]:{еj} %+Ending>	#;
<B [%+Subst2 [%+Fem | %+Neut] %+Plur %+Gen]:{еj}>			#;

LEXICON Subst1
Subst1DirectSing;
+Masc:0 				MascNeutSubst1;
+Neut:0 				MascNeutSubst1;
+Fem:0 					FemSubst1;
SubstNomPlur;
<Gender %+Plur .x. а>	OblPlur;

LEXICON Subst2
+Masc+Sing+Nom:0		#;
+Neut+Sing+Nom:а		#;
+Fem+Sing+Nom			#;
<Gender %+Sing [%+Gen|%+Dat|%+Loc] .x. ы>	#;
+Fem+Sing+Ins:jу							#;
SubstNomPlur;
<Gender %+Plur .x. а>	OblPlur;

LEXICON SubstNomPlur
+Masc+Plur+Nom:ы		#;
+Neut+Plur+Nom:а		#;
+Fem+Plur+Nom:ы			#;

LEXICON Adj
+Masc+Sing+Nom:ый					Stem;
+Masc+Sing+Nom:ой					Ending;
+Neut+Sing+Nom:оjе					OptRefl;
+Fem+Sing+Nom:аjа					OptRefl;
+Fem+Sing+Acc:уjу					OptRefl;
<Gender %+Plur %+Nom .x. {ыjе}>		OptRefl;
AdjObl;
0:ы 					Them;

LEXICON Pron1
Subst1DirectSing;
AdjObl;
0:ы 					Them;

LEXICON Pron2
Subst1DirectSing;
AdjObl;
0:е 					Them;

LEXICON Poss
0:ы 					Them;

LEXICON Subst1DirectSing
+Masc+Sing+Nom:0		#;
+Neut+Sing+Nom:о		#;
+Fem+Sing+Nom:а			#;
+Fem+Sing+Acc:у			#;

LEXICON MascNeutSubst1
+Sing+Gen:а			#;
+Sing+Dat:у			#;
+Sing+Loc:е		#;
+Sing+Ins:ом		#;

LEXICON FemSubst1
+Sing+Gen:ы						#;
<%+Sing[%+Loc|%+Dat] .x. е>	#;
+Sing+Loc:е					#;
+Sing+Ins:оj					#;
+Sing+Ins:оjу					#;

LEXICON AdjObl
<%+Masc|%+Neut .x. 0>	MascNeutAdj;
<%+Fem:0>				FemAdj;

LEXICON MascNeutAdj
+Sing+Gen:ово		OptRefl;
+Sing+Dat:ому		OptRefl;
+Sing+Loc:ом		OptRefl;

LEXICON FemAdj
<%+Sing [%+Gen|%+Dat|%+Loc|%+Ins] .x. {оj}>	OptRefl;
+Sing+Ins:оjу								OptRefl;

LEXICON Them
<[%+Masc|%+Neut] %+Sing %+Ins .x. м>	OptRefl;
<Gender %+Nom %+Plur .x. 0>				OptRefl;
<Gender %+Plur .x. 0>					AdjOblPlur;

LEXICON AdjOblPlur
+Gen:х				OptRefl;
OblPlur;

LEXICON OblPlur
+Loc:х				OptRefl;
+Dat:м				OptRefl;
+Ins:ми				OptRefl;

LEXICON OptRefl
@D.REFL.ON@			#;
@R.REFL.ON@			Postfix;

LEXICON Postfix
0:с'а		#;

LEXICON Stem
+Stem		#;

LEXICON Ending
+Ending		#;

LEXICON Nul
<Gender Number Case .x. 0>	#;

LEXICON Refl
@P.REFL.ON@			Adj;
