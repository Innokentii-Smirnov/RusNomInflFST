source ../../LetterClasses.foma
source ../../ParadigmFeatures/AP/GetAP.foma
define Feature [%+UnstableO|%+Star];

!!!Блок I. Замена членов акцентно-позиционных чередований!!!
define b1r1 ё -> ё̀ || _ Letter+ Acc, Acc Letter* _ ;
define b1r2 е -> ё || _ \е* "|" ?+ Acc ?* %+UnstableO;
define b1 b1r1 .o. b1r2;

!!!Блок II. Замена членов собственно-позиционных чередований!!!
define s1 е|и -> "*" || _ Irr "|" ?* %+Star;
define s2 [..] -> "*" || ь|С _ С "|" ?* %+Star;
define s3 [..] -> ' || П|Т _ "*" [н|л|р|ц];
define s4 о -> "*" || _ Irr С "|" ?* %+Star;
define s5 Г -> "*" || Ш _ Irr С "|" ?* %+Star;
define s6 Г -> ' "*" || _ Irr С "|" ?* %+Star;
define StarInsertion s1 .o. s2 .o. s3 .o. s4 .o. s5 .o. s6;
define b2r2 ь -> ' "*" || _ Sep;
define b2r3 ' -> 0 || _ "*" Sep;
define b2 StarInsertion .o. b2r2 .o. b2r3;

!!!Блок III. Замена я, ю, ё, ё̀!!!
define b3 я -> ' а, ю -> ' у, ё -> ' о Acc, ё̀ -> ' о̀;

!!!Блок IV. Замена и!!!
define b4r1 и -> ы || К "|" _;
define b4r2 и -> е || и "|" _ Unacc ?* "+Sg" ["+Prep"|"+Dat"];
define b4r3 и -> ' ы || "|" _;
define b4r4 [..] -> ' || Г|ь _ и;
define b4 b4r1 .o. b4r2 .o. b4r3 .o. b4r4;

!!!Блок V. Замена е!!!
define b5r2 е -> ' о || "|" _ Unacc;
define b5r3 [..] -> ' || .#. | Г | ь _ е;
define b5 b5r2 .o. b5r3;

!!!Блок VI. Замена безударного о после шипящих и ц!!!
define b6 о -> "о₂" || Ш | ц _ Unacc;

!!!Блок VII. Замена й и ь!!!
define j1 [..] -> j || й _ Sep;
define j2 й -> j || _ \ [Г|j] | .#.;
define j3 ' -> j || .#. _ , \С Sep _ , \[С|Sep] _ ;
define j4 ь -> ' || _ \ о | .#.;
define j5 Sep j -> j Sep;
define j6 ' -> 0 || [Ш|ц|j] Sep _ ;
define j7 Sep ' -> ' Sep;
define b7 j1 .o. j2 .o. j3 .o. j4 .o. j5 .o. j6 .o. j7;

!!!Последний блок!!!
define Accent Acc -> "" || ё _ ;
define Cleanup Feature -> "";
define OrthToConv GetAP .o. b1 .o. b2 .o. b3 .o. b4 .o. b5 .o. b6 .o. b7 .o. Accent .o. Cleanup;
