load stack ../../Accentuation/Accentuate/Accentuate.bin
invert net
define Accentuate;
source ../../LetterClasses.foma
define r1 "*" Irr -> "" || _ С (')("|") Г;
define r2 ' -> "" || \л _ [н|л|р|ц|с|т];
define r3 [..] -> ' || [С - й] _ ("|")j;
define StarDeletion r1 .o. r2 .o. r3;
define r4 "*" -> и || _ j;
define r5 "*" -> е || _ Irr j;
define r6 "*" -> е || [j|'|Ш|ц] _ Irr [ц|л '|н '];
define r7 "*" -> о;
define StarToVowel r4 .o. r5 .o. r6 .o. r7;
define r8 е -> и || и j "|" _ Unacc;
define r9 в -> г || "|" о Irr _ о;
define Orth r8 .o. r9;
define r10 ы -> и || [К|Ш]"|" _;
define r11 о -> е || [j|'|Ш|ц]("|") _ Unacc;
define r12 "о₂" -> о;
define VowelChanges r10 .o. r11 .o. r12;
define r13 ' "|" -> "|" ';
define r14 j "|" -> "|" j;
define r15 [j|']а -> я , [j|']у -> ю , [j|'][о|о̀] -> ё , [j|']е -> е, [j|'][ы|и] -> и;
define r16 j -> й;
define r17 ' -> ь;
define Softness r13 .o. r14 .o. r15 .o. r16 .o. r17;
define Accent Acc -> "" || ё _ .o. Acc -> "́";
define Postprocessing ё (->) е || _ ;
set flag-is-epsilon ON
define ConvToOrth Accentuate .o. StarDeletion .o. StarToVowel .o. Orth .o. VowelChanges .o. Softness .o. Accent .o. Postprocessing;
