load stack ../../ParadigmFeatures/Star/GetStar.bin
invert net
define GetStar;
read lexc ../../ParadigmFeatures/AP/GetAP.lexc
define APLexicon;
source ../../LetterClasses.foma
source ../../ParadigmFeatures.foma
define Features (Star) Alternation* NonStandEnding* DeclType (Gender) (Number) (NumberHom) (ParadigmDefects) GrammRazr (Animacy) (Gender);
define APExceptions [Wordform 0:AP 0:Alternation* (0:%+Nse1) .o. APLexicon / Sep] Feature+;
define StemStressed Letter* AccentedVowel Letter* Sep Letter* | Syll Sep (Consonants);
define EndingStressed Letter* Sep Letter* AccentedVowel Letter* | (Consonants) Sep Syll;
define Monosyll Syll / Sep;
define Disyll Syll^2 / Sep;
define Polysyll Syll^>1 / Sep;
define FinalStressed UnaccOrth+ AccentedVowel С* Sep;
#
# Слова бесчисловых разрядов
regex Wordform %+a (Star) DeclType Gender Number (ParadigmDefects) %+Nesoglbch;
regex Wordform %+a (Star) DeclType Number (ParadigmDefects) %+Soglbch;
union net
define Numless;
#
# Существительные
#
# Существительные несубстантивных типов склонения
define Nul Wordform %+a %+Nul Gender (NumberHom);
define Finals Sep [о Acc й | а Acc я | о Acc е | ы Acc е | и Acc е] | [и Acc н | ы Acc н] Sep | Sep Г Acc;
define Other [Letter+ Finals 0:%+b .P. Wordform 0:%+a].l
	(Star) [DeclType - [Subst | %+Nul]] Gender (NumberHom);
define NonsubstantivalNouns Nul | Other;
# Существительные субстантивных типов склонения
#
# Группа первая. Существительные мужского морфологического рода (кроме pluralia tantum)
regex Letters [щ | Г Irr ч | [у|ю] Irr н]		Sep;
regex Letters {ец} Sep & Monosyll;
regex Syll+	[{е́ц}|{ёж}|{ы́ш}]	Sep;
regex Syll	[{а́ш}|{я́ш}]			Sep;
regex Letters [{р|ь}|{у́л|ь}|{ы́л|ь}] & Polysyll;
regex Syll+	[{я́к}|{ю́к}|{и́к}|Ш {а́к}|Ш {у́к}]		Sep;
regex Syll	[{а́к}|{у́к}|{ы́к}]	Sep;
regex Syll+ {ю́г}				Sep;
union net
regex 0:%+b;
turn stack
concatenate net
regex FinalStressed 0:%+b %+Star;
union net
define g1b;
define g1a Wordform 0:%+a (Star);
define FirstGroup [g1b .P. g1a].l %+Subst1 %+Masc;
#
# Группа вторая. Существительные среднего морфологического рода (кроме pluralia tantum)
regex [Letter+ м Sep я 0:%+c .P. StemStressed 0:%+a].l;
regex EndingStressed %+b;
union net
regex (Star) Subst %+Neut;
turn stack
concatenate net
define SecondGroup;
#
# Группа третья. Существительные женского морфологического рода I субстантивного склонения (кроме pluralia tantum)
regex StemStressed %+a;
regex EndingStressed %+b;
union net
regex (Star) %+Subst1 %+Fem;
turn stack
concatenate net
define ThirdGroup;
#
# Группа четвертая. Существительные женского морфологического рода II субстантивного склонения (кроме pluralia tantum)
define FourthGroup Wordform %+a %+Subst2 %+Fem;
#
# Группа пятая. Pluralia tantum
regex StemStressed %+a;
regex EndingStressed %+b;
union net
regex (Star) [%+Subst1|%+Subst2] Gender %+NumberHom1;
turn stack
concatenate net
define FifthGroup;
#
# Объединение
define SubstantivalNouns [FirstGroup | SecondGroup | ThirdGroup | FourthGroup | FifthGroup];
define Nouns [NonsubstantivalNouns | SubstantivalNouns] (ParadigmDefects) %+Subst;
#
# Прилагательные
#
# Прилагательные простого адъективного разряда
regex [[Letter* Sep о Acc й | Letter* [и|ы] Acc н Sep] 0:%+b .P.
	Wordform 0:%+a].l;
regex (Star) DeclType (ParadigmDefects) %+Simadj;
turn stack
concatenate net
define SimAdjectival;
#
# Прилагательные расширенного адъективного разряда
regex StemStressed %+a;
regex EndingStressed %+b;
union net
regex (Star) DeclType (ParadigmDefects) %+Extadj;
turn stack
concatenate net
define ExtAdjectival;
#
# Объединение
define APDistribution Numless | Nouns | SimAdjectival | ExtAdjectival;
#
define Intervening Alternation* NonStandEnding*;
define Following (Animacy) (Gender);
define ExtendedAPDistribution [APDistribution / Intervening] Following .o. Wordform AP Features;
define APRules Wordform 0:AP Features .o. ExtendedAPDistribution;
define GetAP GetStar .o. APExceptions .P. APRules .P. Wordform 0:%+Error Feature+;
